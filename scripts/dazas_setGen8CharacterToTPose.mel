//Set Genesis 8 Character From Bind Pose to T-Pose.
//Go to Bind Pose then run this script.
global proc dazas_setGen8CharacterToTPose()
{
  string $charObj[] = ls("-sl", "-tr");
  if (size(listRelatives("-s", "-typ", "mesh", $charObj)) == 0)
  {
    string $message =
      "In order to determine the position and angle of the foot, "+
      "the Genesis 8 character mesh needs to be selected.\n"+
      "Continuing without selecting the mesh will not ensure that "+
      "the foot is horizontal and that the bottom of the foot is in the Y=0 plane.\n "+
      "Are you sure?";

    string $yesOrNot = confirmDialog("-t","Warning:", "-m",$message, "-b","Yes", "-b","No", "-db","Yes", "-cb","No", "-ds","No");
    if ($yesOrNot=="No")
      return;
  }

  string $OriginLct[] = spaceLocator();
  string $xdirLct[] = spaceLocator();
  setAttr($xdirLct[0]+".t",1,0,0);
  string $ydirLct[] = spaceLocator();
  setAttr($ydirLct[0]+".t",0,1,0);
  string $nydirLct[] = spaceLocator();
  setAttr($nydirLct[0]+".t",0,-1,0);
  string $zdirLct[] = spaceLocator();
  setAttr($zdirLct[0]+".t",0,0,1);
  string $nzdirLct[] = spaceLocator();
  setAttr($nzdirLct[0]+".t",0,0,-1);

  // spine
  setAttr("hip.tx", 0);
  setAttr("pelvis.tx", 0);
  setAttr("abdomenLower.tx", 0);
  setAttr("abdomenUpper.tx", 0);
  setAttr("chestLower.tx", 0);
  setAttr("chestUpper.tx", 0);
  setAttr("neckLower.tx", 0);
  setAttr("neckUpper.tx", 0);
  setAttr("head.tx", 0);

  string $headFrontLct[] = spaceLocator();
  float $headPos[] = xform("-q", "-t", "-ws", "head");
  setAttr($headFrontLct[0]+".t",$headPos[0],$headPos[1],$headPos[2]+1);
  parent($headFrontLct[0], "head");

  //dazas_aimOrientMatch("hip", "abdomenUpper", "", {0.0,0.0,1.0}, {0.0,0.0,1.0},
  //$OriginLct[0], $ydirLct[0], "", {0.0,0.0,1.0}, {0.0,0.0,1.0}, 1.0, "hip", "abdomenLower");

  dazas_aimOrientMatch("abdomenUpper", "chestLower", "", {0.0,0.0,1.0}, {0.0,0.0,1.0},
  $OriginLct[0], $ydirLct[0], "", {0.0,0.0,1.0}, {0.0,0.0,1.0}, 1.0, "", "abdomenUpper");

  dazas_aimOrientMatch("chestLower", "neckLower", "", {0.0,0.0,1.0}, {0.0,0.0,1.0},
  $OriginLct[0], $ydirLct[0], "", {0.0,0.0,1.0}, {0.0,0.0,1.0}, 1.0, "", "chestLower");

  //dazas_aimOrientMatch("chestUpper", "neckLower", "", {0.0,0.0,1.0}, {0.0,0.0,1.0},
  //$OriginLct[0], $ydirLct[0], "", {0.0,0.0,0.1}, {0.0,0.0,0.1}, 1.0, "", "chestUpper");

  dazas_aimOrientMatch("head", $headFrontLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $zdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "neckLower");
  delete($headFrontLct[0]);

  // arm
  dazas_aimOrientMatch("lCollar", "lShldrBend", "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $xdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lCollar");

  dazas_aimOrientMatch("lShldrBend", "lForearmBend", "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $xdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lShldrBend");

  dazas_aimOrientMatch("lForearmBend", "lHand", "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $xdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lForearmBend");

  rotate("-r", "-ws", "-fo", 0,-2,0, "lForearmBend");

  dazas_aimOrientMatch("lShldrBend", "lHand", "lForearmBend", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $xdirLct[0], $nzdirLct[0], {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lShldrBend");

  dazas_aimOrientMatch("lCarpal2", "lMid4", "lCarpal4", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $xdirLct[0], $nzdirLct[0], {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lHand");

  // leg
  dazas_aimOrientMatch("lThighBend", "lShin", "", {0.0,-1.0,0.0}, {0.0,0.0,1.0},
  $OriginLct[0], $nydirLct[0], "", {0.0,-1.0,0.0}, {0.0,0.0,1.0}, 1.0, "", "lThighBend");

  dazas_aimOrientMatch("lShin", "lFoot", "", {0.0,-1.0,0.0}, {0.0,0.0,1.0},
  $OriginLct[0], $nydirLct[0], "", {0.0,-1.0,0.0}, {0.0,0.0,1.0}, 1.0, "", "lShin");

  // twist leg
  string $lThighBendFrontLct[] = spaceLocator();
  float $lThighBendPos[] = xform("-q", "-t", "-ws", "lThighBend");
  float $lToePos[] = xform("-q", "-t", "-ws", "lToe");
  setAttr($lThighBendFrontLct[0]+".t",$lToePos[0],$lThighBendPos[1],$lToePos[2]);
  dazas_aimOrientMatch("lThighBend", $lThighBendFrontLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $zdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 0.333, "", "lThighBend");
  delete($lThighBendFrontLct[0]);

  $lThighBendFrontLct = spaceLocator();
  $lThighBendPos = xform("-q", "-t", "-ws", "lThighBend");
  $lToePos = xform("-q", "-t", "-ws", "lToe");
  setAttr($lThighBendFrontLct[0]+".t",$lToePos[0],$lThighBendPos[1],$lToePos[2]);
  string $lThighTwistPivotLct[] = spaceLocator();
  float $lThighTwistPos[] = xform("-q", "-t", "-ws", "lThighTwist");
  setAttr($lThighTwistPivotLct[0]+".t",$lThighBendPos[0],$lThighTwistPos[1],$lThighBendPos[2]);
  dazas_aimOrientMatch("lThighBend", $lThighBendFrontLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $zdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 0.5, $lThighTwistPivotLct[0], "lThighTwist");
  delete($lThighTwistPivotLct[0], $lThighBendFrontLct[0]);

  rotate("-r", "-ws", "-fo", 2,0,0, "lShin");

  dazas_aimOrientMatch("lThighBend", "lFoot", "lShin", {0.0,-1.0,0.0}, {0.0,0.0,1.0},
  $OriginLct[0], $nydirLct[0], $zdirLct[0], {0.0,-1.0,0.0}, {0.0,0.0,1.0}, 1.0, "", "lThighBend");

  // feet
  dazas_aimOrientMatch("lToe", "lSmallToe4", "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $xdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lFoot");

  string $lFootFrontLct[] = spaceLocator();
  float $lFootPos[] = xform("-q", "-t", "-ws", "lFoot");
  $lToePos = xform("-q", "-t", "-ws", "lToe");
  setAttr($lFootFrontLct[0]+".t",$lToePos[0],$lFootPos[1],$lToePos[2]);
  dazas_aimOrientMatch("lFoot", $lFootFrontLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
  $OriginLct[0], $zdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lFoot");
  delete($lFootFrontLct[0]);

  if (size($charObj))
  {
    string $lFootOriginLct[] = spaceLocator();
    float  $lFootOriginPos[] = pointPosition("-w", $charObj[0]+".vtx[4462]");
    setAttr($lFootOriginLct[0]+".t",$lFootOriginPos[0],$lFootOriginPos[1],$lFootOriginPos[2]);
    string $lFootAimLct[] = spaceLocator();
    float  $lFootAimPos[] = pointPosition("-w", $charObj[0]+".vtx[2079]");
    setAttr($lFootAimLct[0]+".t",$lFootOriginPos[0],$lFootAimPos[1],$lFootAimPos[2]);
    dazas_aimOrientMatch($lFootOriginLct[0], $lFootAimLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0},
    $OriginLct[0], $zdirLct[0], "", {0.0,1.0,0.0}, {0.0,1.0,0.0}, 1.0, "", "lFoot");
    delete($lFootAimLct[0], $lFootOriginLct[0]);

    float $solesPos[] = pointPosition("-w", $charObj[0]+".vtx[4459]");
    move("-ws", "-r",0,-$solesPos[1],0, "hip");
  }
  else
  {
    warning("\nNo mesh of Genesis 8 Character is selected to determine the angle and location of the sole.");
  }

  dazas_mirrorDazJointsTrans(0);

  delete($OriginLct[0], $xdirLct[0], $ydirLct[0], $nydirLct[0], $zdirLct[0], $nzdirLct[0]);
}
