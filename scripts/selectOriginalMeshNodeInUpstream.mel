// Select Very First Original Mesh Nodes.
// Select a mesh and run this script.
{
    // List Mesh Upstream Geometry Nodes.
    proc string[] listUpstream(string $startNode, string $passedNodes[], int $depth, int $maxDepth)
    {
        string $plugs[];
        if(nodeType($startNode) == "mesh")
        {
            $plugs[0] = $startNode+".inMesh";
        }
        else
        {
            string $plugName[] = listAttr("-a", "-m", "-st", "*inputGeometry*", $startNode);
            int $s = size($plugName);
            for($i=0; $i<$s; $i++)
            {
                $plugs[$i] = $startNode+"."+$plugName[$i];
            }
        }

        $sour = stringArrayRemove($passedNodes, stringArrayRemoveDuplicates(listConnections("-s", 1, "-d", 0, "-scn", 1, "-sh", 1, $plugs)));
        if( size($sour) > 0 )
        {
            $passedNodes = stringArrayCatenate($passedNodes, $sour);
            if( $depth < $maxDepth )
            {
                for( $each in $sour )
                {
                    $passedNodes = listUpstream($each,$passedNodes,($depth+1),$maxDepth);
                }
            }
        }
        return $passedNodes;
    }

    string $selObjShape[] = listRelatives("-s", "-typ", "mesh", ls("-sl"));
    string $upstreamNodes[] =  listUpstream($selObjShape[0], {$selObjShape[0]}, 0, 20);

    int $s = size($upstreamNodes);
    for($i=$s-1; $i>=0; $i--)
    {
        if(nodeType($upstreamNodes[$i]) == "mesh")
        {
            select -r $upstreamNodes[$i];
            break;
        }
    }
}
